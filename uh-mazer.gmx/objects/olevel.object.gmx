<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>slevelgen</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>0</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///create the level

//resize the room
room_width = (CELLWIDTH/16) * 720;
room_height = (CELLHEIGHT/16) * 720;

//set the grid width and height
var width = room_width div CELLWIDTH;
var height = room_height div CELLHEIGHT;

///create grid
grid = ds_grid_create (width, height);

//create pathfinding grid
gridpath = mp_grid_create (0, 0, width, height, CELLWIDTH, CELLHEIGHT);

///fill the void
ds_grid_set_region (grid, 0, 0, width - 1, height - 1, VOID);

///load a room
if show_question ("Would you like to load a room?") {
   var key = get_string ("What is the name of the room", "default")
   ini_open ("save.ini");
   var seed = ini_read_real ("rooms", key, 0);
   random_set_seed (seed);
}else{
    /// randomize the world
    randomize ();
}

//create the controller at the center of the grid 
var cx = width div 2;
var cy = height div 2;

//create the player
instance_create (cx * CELLWIDTH + CELLWIDTH / 2, cy * CELLHEIGHT + CELLHEIGHT / 2, oplayer);

//give the controller a random direction
var cdir = irandom(3);

///odds variable to change direction
var odds = 1;

//create the level uning 1000 steps
repeat (1000) {
       //place a floor tile at the controller position
       grid [# cx, cy] = FLOOR
       
       ///randomize the direction of the controller
       if irandom(odds) == odds {
          cdir = irandom (3);
       
       }
       ///move the controller
       var xdir = lengthdir_x (1, cdir * 90);
       var ydir = lengthdir_y (1, cdir * 90);
       cx += xdir;
       cy += ydir;
       //make sure we dont move outside the grid
       cx = clamp (cx, 1, width - 2);
       cy = clamp (cy, 1, height - 2);
}

//add the walls
for (var yy = 1; yy &lt; height - 1; yy++;) {
    for (var xx = 1; xx &lt; width - 1; xx++;) {
        if grid[# xx, yy] == FLOOR {
           ///check for walls
           if grid[# xx + 1, yy] != FLOOR {
              grid[# xx + 1, yy] = WALL;
           }
           if grid[# xx - 1, yy] != FLOOR {
              grid[# xx - 1, yy] = WALL;
           }
           if grid[# xx, yy + 1] != FLOOR {
              grid[# xx, yy + 1] = WALL;
           }
           if grid[# xx, yy - 1] != FLOOR {
              grid[# xx, yy - 1] = WALL;
           }
        }
    }
}

//delete walls that are alone
for (var yy = 1; yy &lt; height - 1; yy++;) {
    for (var xx = 1; xx &lt; width - 1; xx++;) {
        if grid[# xx, yy] == WALL &amp;&amp; grid[# xx + 1, yy] == FLOOR &amp;&amp; grid[# xx - 1, yy] == FLOOR &amp;&amp; grid[# xx, yy + 1] == FLOOR &amp;&amp; grid[# xx, yy - 1] == FLOOR  {
           grid[# xx, yy] = FLOOR;
        } 
    }
}

///draw the level using the grid
for (var yy = 0; yy &lt; height; yy++) {
    for (var xx = 0; xx &lt; width; xx++){
        ///draw the floor
        if grid[# xx, yy] == FLOOR {
           tile_add (bfloor, 0, 0, CELLWIDTH, CELLHEIGHT, xx * CELLWIDTH, yy * CELLHEIGHT,1000);
           //add some enemys
           var eodds = 20;
           var ex = xx * CELLWIDTH + CELLWIDTH / 2;
           var ey = yy * CELLHEIGHT + CELLHEIGHT / 2;
           if point_distance (ex, ey, oplayer.x, oplayer.y) &gt; 80 &amp;&amp; irandom (eodds) == eodds {
              instance_create (ex, ey, osnake);
           }
           
           ///add exit
           var hodds = 50;
           if point_distance (xx * CELLWIDTH, yy * CELLHEIGHT, oplayer.x, oplayer.y) &gt; 160 &amp;&amp; irandom (hodds) == hodds &amp;&amp; !instance_exists (oexit) {
              instance_create (xx * CELLWIDTH, yy * CELLHEIGHT, oexit);
           } 
        }else{
              mp_grid_add_cell (gridpath, xx, yy);
        }
        //draw wall place
        if grid[# xx, yy] == WALL {
           instance_create (xx * CELLWIDTH, yy * CELLHEIGHT, owall);
        }
    }
}
/// get tile sizes
var tw = CELLWIDTH /  2;
var th = CELLHEIGHT / 2;

///draw the tiles
for (var yy = 0; yy &lt; height * 2; yy++) {
    for (var xx = 0; xx &lt; width * 2; xx++){
        if grid[# xx div 2, yy div 2] == FLOOR {
           ///get tiles x and y
           var tx = xx * tw;
           var ty = yy * th;
           
           var right = grid[# (xx + 1) div 2, yy div 2] != FLOOR;
           var left = grid[# (xx - 1) div 2, yy div 2] != FLOOR;
           var top = grid[# xx div 2, (yy - 1) div 2] != FLOOR;
           var bottom = grid[# xx div 2, (yy + 1) div 2] != FLOOR;
           
           var topright = grid[# (xx + 1) div 2, (yy - 1) div 2] != FLOOR;
           var topleft = grid[# (xx - 1) div 2, (yy - 1) div 2] != FLOOR;
           var bottomright = grid[# (xx + 1) div 2, (yy + 1) div 2] != FLOOR;
           var bottomleft =  grid[# (xx - 1) div 2, (yy + 1) div 2] != FLOOR;
           
           if right {
              if bottom {
                 tile_add (bwalltiles, tw * 4, th * 1, tw, th, tx + tw, ty, -ty);
              }else if top {
                    if topright {
                       tile_add (bwalltiles, tw * 4, th * 0, tw, th, tx + tw, ty - th, -ty + 4);
                    }else{
                          tile_add (bwalltiles, tw * 3, th * 0, tw, th, tx, ty - th, -ty + 4);
                    }
                    tile_add (bwalltiles, tw * 0, th * 1, tw, th, tx + tw, ty, -ty);
              }else{
                    tile_add (bwalltiles, tw * 0, th * 1, tw, th, tx + tw, ty, -ty);
              }
           }
           
           if left {
              if bottom {
                 tile_add (bwalltiles, tw * 3, th * 1, tw, th, tx - tw, ty, -ty);
              }else if top {
                    if topleft {
                       tile_add (bwalltiles, tw * 3, th * 0, tw, th, tx - tw, ty - th, -ty + 4);
                    }else{
                       tile_add (bwalltiles, tw * 4, th * 0, tw, th, tx, ty - th, -ty + 4);
                    }
                    tile_add (bwalltiles, tw * 2, th * 1, tw, th, tx - tw, ty, -ty);
              }else{
                    tile_add (bwalltiles, tw * 2, th * 1, tw, th, tx - tw, ty, -ty);
              }
           }
           
           if top {
              if !topright {
                  tile_add (bwalltiles, tw * 2, th * 2, tw, th, tx, ty - th, -ty + 4);
              } else if !topleft {
                  tile_add (bwalltiles, tw * 0, th * 2, tw, th, tx, ty - th, -ty + 4);
              }else{
                  tile_add (bwalltiles, tw * 1, th * 2, tw, th, tx, ty - th, -ty + 4);
              }  
           }
           
           if bottom {
              if !bottomright {
                  tile_add (bwalltiles, tw * 2, th * 0, tw, th, tx, ty, -ty - tw);
              } else if !bottomleft {
                  tile_add (bwalltiles, tw * 0, th * 0, tw, th, tx, ty, -ty - tw);
              }else{
                  tile_add (bwalltiles, tw * 1, th * 0, tw, th, tx, ty, -ty - tw);
              }
           } 
        }
    }
}

</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///draw invisible
if DEBUG == true { draw_self(); }

</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="9" enumb="81">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///save the room
ini_open ("save.ini");
var key = get_string ("What will you name the room", "default");
var val = random_get_seed();
ini_write_real ("rooms" , key, val);
ini_close();
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
